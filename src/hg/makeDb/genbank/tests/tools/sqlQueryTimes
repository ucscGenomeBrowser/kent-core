#!/usr/bin/awk -f
#
# generate reports for logs files based on:
#  JKSQL_TRACE=on
#  JKSQL_PROF=on
#

# used connection number to match time to query, allowing
# for more than one query to be active.
BEGIN {
    OFS="\t";
}


/SQL_/ {
    conn = $2;
}

# SQL_QUERY 249 hg17 select count(*) from gbSeq
/SQL_QUERY/ {
    q = $4;
    for (i = 5; i <= NF; i++) {
        q = q " " $i;
    }
    queries[conn] = q;
}

# SQL_TIME 249 hg17 0.000s
/SQL_TIME/ {
    print gensub("s$", "", "g", $4), queries[conn];
    queries[conn] = "";
}

# SQL_DISCONNECT 249 hg17
#  don't really cared for now
