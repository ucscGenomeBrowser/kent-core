#!/usr/bin/awk -f

# Convert bed file to a genePred file (on stdout)
# Ignore score and reserved
#
BEGIN {
    FS="\t";
    OFS="\t";
}
{
    chrom=$1
    start=$2
    end=$3
    name=$4
    strand=$6
    cdsStart=$7
    cdsEnd=$8
    blkCnt=$10

    delete sizes
    split($11, sizes, ",");
    delete starts
    split($12, starts, ",");

    exonStarts=start ",";
    exonEnds=(start + sizes[1]) ",";
    for (i = 2; i <= blkCnt; i++) {
        exonStarts = exonStarts (start + starts[i]) ",";
        exonEnds = exonEnds (start + starts[i] + sizes[i]) ",";
    }
    
    print name, chrom, strand, start, end, cdsStart, cdsEnd, blkCnt, exonStarts, exonEnds
}
