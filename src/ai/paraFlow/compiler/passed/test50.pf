to wordCount(string s) into int count
   {
   int pos,string w;
   count = 0;
   for (;;)
       {
       (pos,w) = s.nextWord(pos);
       if (!w)
           break;
       count += 1;
       }
   }
       
to split(string s) into array of string words
   {
   int count = wordCount(s);
   array[count] of string a;
   int i, pos, string word;
   for (i=0; i<count; i += 1)
       {
       (pos,word) = s.nextWord(pos);
       a[i] = word;
       }
   words = a;
   }

to hashFunc(string s) into int val
    {
    foreach byte c in s
        {
	val *= 5;
	val += c;
	}
    }

class hashEl
    {
    hashEl next;
    string name;
    int count;
    to find(string name) into (hashEl match)
        {
	hashEl h = self;
	while (h)
	    {
	    if (name == h.name)
		{
		match = h;
		return;
		}
	    h = h.next;
	    }
	}
    }

class hash
    {
    array of hashEl table;	// Dynamically allocated.
    int size;			// Size of table.
    to find(string name) into (hashEl match)
        {
	int ix = hashFunc(name) % self.size;
	hashEl hel = self.table[ix];
	match = hel.find(name);
	// match = self.table[ix].find(name);
	}
    to add(string name) into (hashEl hel)
        {
	int ix = hashFunc(name) % self.size;
	hel = (self.table[ix], name, 0);
	self.table[ix] = hel;
	}
    to addCount(string name)
        {
	hashEl hel = self.find(name);
	if (!hel)
	    hel = self.add(name);
	hel.count += 1;
	}
    to findCount(string name) into (int count)
        {
	hashEl hel = self.find(name);
	if (hel)
	    count = hel.count;
	}
    };

to createHash(int size) into (hash hash)
    {
    array[size] of hashEl table;
    hash = (table, size);
    }

hash hash = createHash(32);
hash.addCount("Mary");
hash.addCount("Mary");
hash.addCount("Jim");
hash.addCount("Jack");
hash.addCount("Jim");
hash.addCount("Sam");

print("Jim is used " + hash.findCount("Jim") + " times.");
print("Mary is used " + hash.findCount("Mary") + " times.");
print("Sam is used " + hash.findCount("Sam") + " times.");
print("Denise is used " + hash.findCount("Denise") + " times.");

/*-JUST_EXAMPLE

string s = "My name is Jim.";
print(s + " has " + wordCount(s) + " words");
print(split(s));

to makeList(array of string a) into hashEl lis
    {
    foreach string s in a
        {
	hashEl el = (lis, s, s+" val");
	lis = el;
	}
    }

string joe = "Joe";
array of string a = ("John", "Mary", joe, "Johnny", "Jack");
hashEl lis = makeList(a);
print(a);
print(lis);
hashEl joeEl = lis.find(joe);
prin("joeEl = ");
print(joeEl);
JUST_EXAMPLE-*/
