
global interface stream
    {
    to read(int size) into string s;
    to write(string s);
    to close();
    }

class stringStream
    {
    dyString buf;

    to read(int size) into string s
        {
	if (buf.size <= size)
	    {
	    s = buf;
	    buf = "";
	    }
	else
	    {
	    s = buf.first(size);
	    buf = buf.rest(size);
	    }
	}

    to write(string s)
        {
	buf += s;
	}

    to close()
        {
	buf = nil;
	}
    }

global to openStringStream(string string) into (stream stream)
    {
    stringStream ss = (string);
    stream = ss;
    }

class fileStream
    {
    file f;

    to read(int size) into string s
        {
	s = f.read(size);
	}

    to write(string s)
        {
	f.write(s);
	}

    to close()
        {
	if (f)
	    {
	    f.close();
	    f = nil;
	    }
	}
    }

global to openFileStream(string fileName, string mode="r") into stream stream
    {
    fileStream fs = (fileOpen(fileName, mode),);
    stream = fs;
    }
