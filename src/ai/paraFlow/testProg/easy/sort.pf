flow cmpIntRev(int a,b) into int c
    {
    c = b-a;
    }
array of int a = (3,2,6,5,1,4);
a.append(666);
a.push(123);
prin("original: "); print(a);
a.sort();
prin("forward: "); print(a);
a.sort(cmpIntRev);
prin("reverse: "); print(a);

flow cmpStringSize(string a,b) into int diff
    {
    diff = a.size - b.size;
    }
array of string names = ("Jim", "Ann", "Marty", "Simon", "Paul", "Charlotte");
prin("original: ");print(names);
names.sort();
prin("sorted alphabetically: ");print(names);
names.sort(cmpStringSize);
prin("sorted by size: "); print(names);

array of double costs = (1.2, 3.4, 5.5, -1, 4.3);
prin("original: ");print(costs);
costs.sort();
prin("sortedl: ");print(costs);

class person
    {
    string firstName, middleNames, lastName;
    };

flow cmpString(string a,b) into int diff
    {
    if (a<b)
        diff = -1;
    else if (a == b)
        diff = 0;
    else
        diff = 1;
    }

flow cmpPerson(person a,b) into int diff
    {
    diff = cmpString(a.lastName, b.lastName);
    if (diff == 0)
	{
        diff = cmpString(a.firstName, b.firstName);
	if (diff == 0)
	    diff = cmpString(a.middleNames, b.middleNames);
	}
    }

array of person staff = (
    ("Joseph", "Allen", "Bates"), 
    ("Erik", "Thurgood", "Anderson"),
    ("Warren", "G.", "Harding"),
    ("Simon", "", "Green")
    );
prin("unsorted: "); print(staff);
staff.sort(cmpPerson);
prin("sorted: "); print(staff);
