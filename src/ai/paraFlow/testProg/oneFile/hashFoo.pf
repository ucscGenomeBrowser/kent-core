// This module creates a hash to count all of the words in a file
// named foo.  It then prints out how many times the words
// 'the' 'into' 'of' and 'love' are used.

to wordCount(string s) into int count
   {
   int pos,string w;
   count = 0;
   for (;;)
       {
       (w, pos) = s.nextWord(pos);
       if (!w)
           break;
       count += 1;
       }
   }
       
to split(string s) into array of string words
   {
   int count = wordCount(s);
   array[count] of string a;
   int i, pos, string word;
   for (i=0; i<count; i += 1)
       {
       (word,pos) = s.nextWord(pos);
       a[i] = word;
       }
   words = a;
   }

to hashFunc(string s) into int val
    {
    for (byte c in s)
        {
	val *= 5;
	val += c;
	}
    val = val & 0xffffff;
    }

class hashEl
    {
    hashEl next;
    string name;
    int count;
    to find(string name) into (hashEl match)
        {
	hashEl h = self;
	while (h)
	    {
	    if (name == h.name)
		{
		match = h;
		return;
		}
	    h = h.next;
	    }
	}
    }

class hash
    {
    array of hashEl table;	// Dynamically allocated.
    int size;			// Size of table.
    to find(string name) into (hashEl match)
        {
	int ix = hashFunc(name) % self.size;
	hashEl hel = self.table[ix];
	match = hel.find(name);
	// match = self.table[ix].find(name);
	}
    to add(string name) into (hashEl hel)
        {
	int ix = hashFunc(name) % self.size;
	hel = (self.table[ix], name, 0);
	self.table[ix] = hel;
	}
    to addCount(string name)
        {
	hashEl hel = self.find(name);
	if (!hel)
	    hel = self.add(name);
	hel.count += 1;
	}
    to findCount(string name) into (int count)
        {
	hashEl hel = self.find(name);
	if (hel)
	    count = hel.count;
	}
    };

to createHash(int size) into (hash hash)
    {
    array[size] of hashEl table;
    hash = (table, size);
    }


hash hash = createHash(50000);
string fileName = "foo";
if (args.size > 0)
    fileName = args[0];
file f = fileOpen(fileName, "r");
string line;
for (;;)
   {
   line = f.readLine();
   if (!line)
       break;
   array of string words = split(line);
   for (string s in words)
       hash.addCount(s);
   }

print("into is used " + hash.findCount("into") + " times.");
print("of is used " + hash.findCount("of") + " times.");
print("the is used " + hash.findCount("the") + " times.");
print("love is used " + hash.findCount("love") + " times.");
